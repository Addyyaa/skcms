load(":ndk_cc_toolchain_config.bzl", "ndk_cc_toolchain_config")

package(default_visibility = ["//visibility:public"])

toolchain(
    name = "linux_ndk_arm64_toolchain",
    # Where should we run this toolchain?
    exec_compatible_with = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
    # What can this toolchain build?
    target_compatible_with = [
        "@platforms//os:android",
        "@platforms//cpu:arm64",
    ],
    toolchain = ":linux_ndk_arm64",
    # https://github.com/bazelbuild/rules_cc/blob/8bb0eb5c5ccd96b91753bb112096bb6993d16d13/cc/BUILD#L32-L36
    toolchain_type = "@rules_cc//cc:toolchain_type",
)

toolchain(
    name = "linux_ndk_arm32_toolchain",
    exec_compatible_with = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
    target_compatible_with = [
        "@platforms//os:android",
        "@platforms//cpu:armv7",
    ],
    toolchain = ":linux_ndk_arm32",
    toolchain_type = "@rules_cc//cc:toolchain_type",
)

filegroup(name = "not_implemented")

############################
# arm64-v8a C++ toolchain. #
############################

# https://bazel.build/reference/be/c-cpp#cc_toolchain
cc_toolchain(
    name = "linux_ndk_arm64",
    all_files = ":not_implemented",
    ar_files = ":ndk_arm64_v8a_toolchain_all_files",
    compiler_files = ":ndk_arm64_v8a_toolchain_all_files",
    dwp_files = ":not_implemented",
    dynamic_runtime_lib = "@ndk_linux_amd64//:arm64_v8a_dynamic_runtime_libraries",
    linker_files = ":ndk_arm64_v8a_toolchain_all_files",
    objcopy_files = ":not_implemented",
    static_runtime_lib = "@ndk_linux_amd64//:arm64_v8a_static_runtime_libraries",
    strip_files = ":not_implemented",
    supports_param_files = False,
    toolchain_config = ":ndk_arm64_v8a_toolchain_config",
)

filegroup(
    name = "ndk_arm64_v8a_toolchain_all_files",
    srcs = glob(["android_trampolines/*.sh"]) + ["@ndk_linux_amd64//:arm64_v8a_all_files"],
)

ndk_cc_toolchain_config(
    name = "ndk_arm64_v8a_toolchain_config",
    cpu = "arm64-v8a",
)

##############################
# armeabi-v7a C++ toolchain. #
##############################

# https://bazel.build/reference/be/c-cpp#cc_toolchain
cc_toolchain(
    name = "linux_ndk_arm32",
    all_files = ":ndk_armeabi_v7a_toolchain_all_files",
    ar_files = ":ndk_armeabi_v7a_toolchain_all_files",
    compiler_files = ":ndk_armeabi_v7a_toolchain_all_files",
    dwp_files = ":ndk_armeabi_v7a_toolchain_all_files",
    dynamic_runtime_lib = "@ndk_linux_amd64//:armeabi_v7a_dynamic_runtime_libraries",
    linker_files = ":ndk_armeabi_v7a_toolchain_all_files",
    objcopy_files = ":ndk_armeabi_v7a_toolchain_all_files",
    static_runtime_lib = "@ndk_linux_amd64//:armeabi_v7a_static_runtime_libraries",
    strip_files = ":ndk_armeabi_v7a_toolchain_all_files",
    supports_param_files = False,
    toolchain_config = ":ndk_armeabi_v7a_toolchain_config",
    toolchain_identifier = "ndk-armeabi_v7a-toolchain",
)

filegroup(
    name = "ndk_armeabi_v7a_toolchain_all_files",
    srcs = glob(["android_trampolines/*.sh"]) + ["@ndk_linux_amd64//:armeabi_v7a_all_files"],
)

ndk_cc_toolchain_config(
    name = "ndk_armeabi_v7a_toolchain_config",
    cpu = "armeabi-v7a",
)
